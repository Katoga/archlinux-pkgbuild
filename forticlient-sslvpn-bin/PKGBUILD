# Maintainer: Katoga <katoga.cz@hotmail.com>
pkgname=forticlient-sslvpn-bin
pkgver=4.4.2327
pkgrel=2
#pkgdesc=""
arch=('i686' 'x86_64')
url="https://hadler.me/linux/forticlient-sslvpn-deb-packages/"
license=('EULA')
depends=('ppp' 'iproute2')
conflicts=('forticlientsslvpn')
replaces=('forticlientsslvpn')

source_x86_64=(https://hadler.me/files/${pkgname:0:(-4)}_${pkgver}-${pkgrel}_amd64.deb)
sha1sums_x86_64=('f4a9d5fcee955ed555b26a02da9c3595aa1ba8f1')
source_i686=(https://hadler.me/files/${pkgname:0:(-4)}_${pkgver}-${pkgrel}_i386.deb)
sha1sums_i686=('c606df3f2ceaf913cbda8cddccf75bbb97c0ec68')

if [[ ${CARCH} == 'x86_64' ]]; then
	_arch=64bit
else
	_arch=32bit
fi

source=(${pkgname}.png)
sha1sums=('2477e22435e57252c42c8c3c1e2ae55ca678584b')

package() {
	# unpack and verify source
	tar -xJf data.tar.xz
	tar -xzf control.tar.gz ./md5sums
	md5sum -c md5sums > /dev/null

	# move licence file to proper directory
	install -Dm644 ${srcdir}/usr/share/doc/forticlient-sslvpn/copyright ${pkgdir}/usr/share/licenses/${pkgname}/EULA

	install -Dm644 ${srcdir}/usr/share/applications/forticlient-sslvpn.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
	# remove she ".sh" extension from executable, no need to decide what architecture dir to enter
	sed -i "s~forticlientsslvpn.sh~forticlientsslvpn~" ${pkgdir}/usr/share/applications/${pkgname}.desktop
	# replace icon with own one
	sed -i "s~forticlientsslvpn.gif~${pkgname}.png~" ${pkgdir}/usr/share/applications/${pkgname}.desktop
	# modify path
	sed -i "s~/opt/forticlient-sslvpn~/opt/${pkgname}~" ${pkgdir}/usr/share/applications/${pkgname}.desktop

	# program icon
	install -Dm644 ${srcdir}/${pkgname}.png ${pkgdir}/opt/${pkgname}/${pkgname}.png

	# move all files from proper architecture directory to destination
	cd ./opt/forticlient-sslvpn/${_arch}
	find . -type f -exec install -D "{}" "$pkgdir/opt/$pkgname/{}" \;
	cd - > /dev/null
}
